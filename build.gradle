buildscript {
    ext {
        // Misc
        kotlin_version = "1.2.60"
        release_plugin_version = "1.9.2"
        ktlint_version = "0.24.0"
        dokka_version = "0.9.17"
        android_gradle_version = "3.2.0-rc02"

        // Implementation
        rxjava_version = "2.2.0"

        // Test
        junit_version = "4.12"
        assertk_version = "0.10"
        mockito_version = "2.19.0"
        mockito_kotlin_version = "2.0.0-RC1"
    }
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "pl.allegro.tech.build:axion-release-plugin:$release_plugin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        classpath "com.android.tools.build:gradle:$android_gradle_version"
    }
}

apply plugin: "pl.allegro.tech.build.axion-release"

scmVersion {
    tag {
        prefix = ""
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
}

subprojects {
    group = "se.gustavkarlsson.krate"
    version = scmVersion.version
}

scmVersion {
    tag {
        prefix = ''
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:$ktlint_version"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "**/src/**/*.kt"
}

task check(group: "verification", dependsOn: ktlint)

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.github.shyiko.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "**/src/**/*.kt"
}
